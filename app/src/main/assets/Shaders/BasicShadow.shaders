#shader vertex
#version 310 es
#extension GL_ANDROID_extension_pack_es31a : enable

in vec4 a_Position;
in vec2 a_TexCoord;
in vec3 a_Normal;
in vec4 a_Color;
in float a_TextureSlot;

out vec2 v_TexCoord;
out vec3 v_Normal;
out vec3 v_CurrentPosition;
out vec4 v_Color;
out float v_TextureSlot;

uniform mat4 mvpmatrix;

void main()
{
    gl_Position = mvpmatrix * vec4(a_Position.x, a_Position.y, a_Position.z, 1.0);

    v_CurrentPosition = vec3(a_Position);
    v_TexCoord = a_TexCoord;
    v_Normal = a_Normal;
    v_Color = a_Color;
    v_TextureSlot =  a_TextureSlot;
}


#shader fragment
#version 310 es
#extension GL_ANDROID_extension_pack_es31a : enable

precision mediump float;

in vec2 v_TexCoord;
in vec3 v_Normal;
in vec3 v_CurrentPosition;
in vec4 v_Color;
in float v_TextureSlot;

uniform sampler2D u_Textures[4];
uniform samplerCube u_ShadowCubeMap;
uniform float u_LightCount;
uniform vec3 u_LightPosition[4];
uniform vec4 u_LightColor[4];
uniform float u_LightIntensity[4];

out vec4 color;

float calculateShadow(int cubeIndex)
{
        if(u_LightPosition[cubeIndex].y < 0.0)
            return 1.0;

        float farPlane = 1000.0;
        float shadow = 0.0;
        vec3 fragToLight = v_CurrentPosition - u_LightPosition[cubeIndex];
        float currentDepth = length(fragToLight);
        float bias = max(0.5 * (1.0 - dot(v_Normal, normalize(u_LightPosition[cubeIndex] - v_CurrentPosition))), 0.005);

        float sampleRadius = 0.0;
        float offset = 0.02;
        for(float z = -sampleRadius; z <= sampleRadius; z++)
        {
        	for(float y = -sampleRadius; y <= sampleRadius; y++)
        	{
        	    for(float x = -sampleRadius; x <= sampleRadius; x++)
        	    {
        	        float closestDepth = texture(u_ShadowCubeMap, fragToLight + vec3(x, y, z) * offset).r;

        			closestDepth *= farPlane;
        			if (currentDepth > closestDepth + bias)
        				shadow += 1.0;
        	    }
        	}
        }
        // Average shadow
        shadow /= pow((sampleRadius * 2.0 + 1.0), 3.0);

        return shadow;
}

float diffuseLighting(int lightIndex)
{
         if(u_LightPosition[lightIndex].y < v_CurrentPosition.y)
            return 0.0;

        vec3 lightDirection = u_LightPosition[lightIndex] - v_CurrentPosition;

        vec3 n_lightDirection = normalize(lightDirection);
	    float diffuse = dot(v_Normal, n_lightDirection);
	    diffuse = max(diffuse, 0.0);

        float distance = length(lightDirection);
        float intensity = 1.0 / u_LightIntensity[lightIndex];
        float attenuation = clamp(1.0 / (1.0 +  intensity * distance + 3.0 * (intensity * intensity) * (distance * distance)), 0.0, 1.0);
        diffuse *= attenuation;

	    return diffuse;
}

void main()
{
        int index = int(v_TextureSlot);
        vec4 texColor = texture(u_Textures[index], v_TexCoord);

        float a = 1.0 - (1.0 - texColor.w) * (1.0 - v_Color.w);
        float r = (v_Color.r * (1.0 - texColor.w) + texColor.r * texColor.w);
        float g = (v_Color.g * (1.0 - texColor.w) + texColor.g * texColor.w);
        float b = (v_Color.b * (1.0 - texColor.w) + texColor.b * texColor.w);

        if(texture(u_Textures[index], v_TexCoord).a == 0.0)
            discard;

        if(u_LightCount == 0.0){
            color = vec4(r, g, b, a);
        }
        else {
            float ambient = 0.35;

            float diffuse = 0.0;
            float shadow = 1.0;

            int lightCount = int(u_LightCount);
            if(lightCount >= 1){
                diffuse += diffuseLighting(0);
                shadow = min(shadow, calculateShadow(0));
            }
            if(lightCount >= 2){
                diffuse += diffuseLighting(1);
                shadow = min(shadow, calculateShadow(1));
            }
            if(lightCount >= 3){
                diffuse += diffuseLighting(2);
                shadow = min(shadow, calculateShadow(2));
            }
            if(lightCount >= 4){
                diffuse += diffuseLighting(3);
                shadow = min(shadow, calculateShadow(3));
            }

            diffuse = min(diffuse, 1.0);

            vec4 lightColor = u_LightColor[0] * (diffuse * (1.0 - shadow) + ambient);
            lightColor.w = 1.0;

            color = vec4(r, g, b, a) * lightColor;
        }
}